plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom' version '0.10-SNAPSHOT' apply false
    id 'com.matthewprenger.cursegradle' version "1.4.0"
}

class Globals {
    static def baseVersion = "1.4.6+1.17"
    static def mcVersion = "1.17.1"
    static def yarnVersion = "+build.61"
}

archivesBaseName = "Server-Translations"

allprojects {
    apply plugin: 'fabric-loom'

    version = Globals.baseVersion
    sourceCompatibility = 16
    targetCompatibility = 16

    group = "fr.catcore"

    repositories {
        mavenCentral()
        maven { url = 'https://maven.nucleoid.xyz/' }
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'SpongePowered'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven {
            name = 'mojang'
            url = 'https://libraries.minecraft.net/'
        }
        mavenLocal()
    }

    dependencies {
        minecraft "com.mojang:minecraft:$Globals.mcVersion"
        mappings "net.fabricmc:yarn:${Globals.mcVersion}${Globals.yarnVersion}:v2"
        modCompileOnly "net.fabricmc:fabric-loader:0.11.6"
//        modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:0.40.1+1.17"
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this task, sources will not be generated.
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    jar {
        from "LICENSE"
    }

    if (it.name != "api") remapJar.dependsOn ':api:remapJar'
}

dependencies {
    implementation project(':api')
    include project(':api'/*name: 'server-translations-api', version: project(':api').version, ext: 'jar'*/)
}
